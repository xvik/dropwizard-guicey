plugins {
    id 'com.gradle.build-scan' version '1.16'
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.1.2'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.quality' version '3.1.1'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.researchgate.release' version '2.7.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'ru.vyarus.mkdocs' version '1.1.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '4.8.1'
}

ext {
    dropwizard = '1.3.7'
    guice = '4.2.2'
    hk2 = '2.5.0-b32'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencyManagement {
    imports {
        mavenBom "com.google.inject:guice-bom:$guice"
        mavenBom "io.dropwizard:dropwizard-bom:$dropwizard"
    }
    // exclusions here mostly fixes conflicts for maven projects
    dependencies {
        // force guava version from dropwizard bom
        dependency "com.google.guava:guava:${dependencyManagement.importedProperties['guava.version']}"
        dependency "org.glassfish.hk2:guice-bridge:$hk2", {
            exclude 'com.google.inject:guice'
            exclude 'org.glassfish.hk2:hk2-api'
        }
        dependency 'org.spockframework:spock-core:1.2-groovy-2.5'
        dependency 'com.github.stefanbirkner:system-rules:1.17.1'

        dependency "com.google.inject:guice:$guice", { exclude 'com.google.guava:guava' }
    }
}
dependencies {
    provided 'junit:junit'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.spockframework:spock-core'
    provided 'com.github.stefanbirkner:system-rules'
    provided 'com.github.spotbugs:spotbugs-annotations:3.1.2'
    provided "org.glassfish.hk2:guice-bridge"

    compile 'com.google.inject:guice'
    compile 'com.google.inject.extensions:guice-servlet'
    compile 'io.dropwizard:dropwizard-core'
    compile 'ru.vyarus:generics-resolver:3.0.0'

    testCompile 'io.dropwizard:dropwizard-auth'
    testCompile 'org.glassfish.jersey.ext:jersey-proxy-client'
    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs.publish {
    docPath = '4.2.2'
    rootRedirect = true
}

pom {
    delegate.properties {
        'guice.version' guice
        'dropwizard.version' dropwizard
        'hk2.version' hk2
    }
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'guice']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    if (System.getenv("TRAVIS")) {
        // tests run only on windows build due to untraceable failure on travis
        exclude "**/HKScopeTest.class"
        exclude "**/HkFirstModeScopeTest.class"
    }
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}
