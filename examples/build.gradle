plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '3.0.2' apply false
    id 'org.openapi.generator' version '6.4.0' apply false
}

wrapper {
    gradleVersion = '8.11'
}

description = 'Dropwizard guicey examples'

ext {
    guiceyBom = '6.3.0-rc.1'
    dwVersion = '3.0.13'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'project-report'

    java {
        sourceCompatibility = 11
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories { mavenLocal(); mavenCentral()}
    dependencies {
        implementation platform("ru.vyarus.guicey:guicey-bom:$guiceyBom")

        constraints {
            implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:5.1.4'

            implementation 'org.flywaydb:flyway-core:9.22.3'
            // flyway not compatible with h2 1.4.200 anymore
            implementation 'com.h2database:h2:2.3.232'
        }

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'ru.vyarus:spock-junit5:1.2.0'
        testImplementation 'org.spockframework:spock-core:2.4-M5-groovy-4.0'
        testImplementation "io.dropwizard:dropwizard-testing"
        testImplementation 'org.junit.jupiter:junit-jupiter-api'

        // required for junit 5.12 in current gradle version
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
        maxHeapSize = "512m"
    }

}

dependencyUpdates.revision = 'release'
