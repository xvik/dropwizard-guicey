plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.2'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.animalsniffer' version '1.1.0'
    id 'ru.vyarus.quality' version '1.3.0'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.6'
    id 'net.researchgate.release' version '2.4.0'
    id 'com.github.ben-manes.versions' version '0.12.0'
}

sourceCompatibility = 1.7

wrapper {
    gradleVersion = 2.14
}

ext {
    dropwizard = '0.9.2'
    guice = '4.1.0'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    signature 'org.codehaus.mojo.signature:java17:+@signature'

    provided 'junit:junit:4.12'
    provided "io.dropwizard:dropwizard-testing:$dropwizard"
    provided 'org.spockframework:spock-core:1.0-groovy-2.4'
    provided 'com.google.code.findbugs:annotations:3.0.1'

    compile "com.google.inject.extensions:guice-servlet:$guice"
    compile "com.google.inject.extensions:guice-multibindings:$guice"
    compile "io.dropwizard:dropwizard-core:$dropwizard"
    compile 'org.glassfish.hk2:guice-bridge:2.4.0-b31'
    compile 'ru.vyarus:generics-resolver:2.0.1'

    testCompile "io.dropwizard:dropwizard-auth:$dropwizard"
    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id "xvik"
            name "Vyacheslav Rusakov"
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'guice']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standard_error"
        exceptionFormat "full"
    }
    maxHeapSize = "512m"
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') << {
    java.awt.Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
}
