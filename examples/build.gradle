plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '4.0.0' apply false
    id 'org.openapi.generator' version '7.12.0' apply false
}

wrapper {
    gradleVersion = '8.10.1'
}

description = 'Dropwizard guicey examples'

ext {
    guiceyBom = findProperty('guiceyBom') ?: '8.0.2'
    dwVersion = findProperty('dropwizardVersion') ?: '5.0.1'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'project-report'

    java {
        sourceCompatibility = 17
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Central Portal Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
            mavenContent {
                snapshotsOnly()
                includeGroupAndSubgroups('ru.vyarus')
            }
        }
    }
    dependencies {
        implementation platform("ru.vyarus.guicey:guicey-bom:$guiceyBom")

        constraints {
            implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:6.0.1'

            implementation 'org.flywaydb:flyway-core:10.18.0'
            // flyway not compatible with h2 1.4.200 anymore
            implementation 'com.h2database:h2:2.3.232'
        }

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'ru.vyarus:spock-junit5:1.2.0'
        testImplementation 'org.spockframework:spock-core:2.4-M5-groovy-4.0'
        testImplementation "io.dropwizard:dropwizard-testing"
        testImplementation 'org.junit.jupiter:junit-jupiter-api'

        // required for junit 5.12 in current gradle version
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
        maxHeapSize = "512m"
    }

}

dependencyUpdates.revision = 'release'
