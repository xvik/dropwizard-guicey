plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '2.2.1'
    id 'ru.vyarus.github-info' version '1.3.0'
    id 'ru.vyarus.quality' version '4.6.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'ru.vyarus.mkdocs' version '2.1.1'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.9'
}

ext {
    dropwizard = '2.0.24'
    guice = '5.0.1'
    hk2 = '2.6.1'
    groovy = '2.5.14'
}

repositories { mavenLocal(); mavenCentral() }
dependencyManagement {
    imports {
        mavenBom "com.google.inject:guice-bom:$guice"
        mavenBom "io.dropwizard:dropwizard-dependencies:$dropwizard"
    }
    // exclusions here mostly fixes conflicts for maven projects
    dependencies {
        // force guava version from dropwizard bom
        dependency "com.google.guava:guava:${dependencyManagement.importedProperties['guava.version']}"
        dependency "org.glassfish.hk2:guice-bridge:$hk2", {
            exclude 'com.google.inject:guice'
            exclude 'org.glassfish.hk2:hk2-api'
        }
        dependency 'org.spockframework:spock-core:1.3-groovy-2.5'
        // groovy bundled with spock (2.5.2) does not work well with java 11
        dependency "org.codehaus.groovy:groovy:$groovy"
        dependency "org.codehaus.groovy:groovy-json:$groovy"
        dependency "org.codehaus.groovy:groovy-macro:$groovy"
        dependency "org.codehaus.groovy:groovy-nio:$groovy"
        dependency "org.codehaus.groovy:groovy-sql:$groovy"
        dependency "org.codehaus.groovy:groovy-templates:$groovy"
        dependency "org.codehaus.groovy:groovy-test:$groovy"
        dependency "org.codehaus.groovy:groovy-xml:$groovy"

        dependency 'com.github.stefanbirkner:system-rules:1.19.0'

        dependency "com.google.inject:guice:$guice", { exclude 'com.google.guava:guava' }

        // add guicey itself to BOM (for version management)
        dependency 'ru.vyarus:dropwizard-guicey:${project.version}'
    }
}
dependencies {
    provided 'junit:junit'
    provided 'org.junit.jupiter:junit-jupiter-api'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.spockframework:spock-core'
    provided 'com.github.stefanbirkner:system-rules'
    provided 'com.github.spotbugs:spotbugs-annotations:4.4.0'
    provided "org.glassfish.hk2:guice-bridge"

    implementation 'com.google.inject:guice'
    implementation 'com.google.inject.extensions:guice-servlet'
    implementation 'io.dropwizard:dropwizard-core'
    implementation 'ru.vyarus:generics-resolver:3.0.3'

    testImplementation 'org.objenesis:objenesis:3.2'
    testImplementation 'org.glassfish.jersey.inject:jersey-hk2'
    testImplementation 'io.dropwizard:dropwizard-auth'
    testImplementation 'org.glassfish.jersey.ext:jersey-proxy-client'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation 'org.junit.platform:junit-platform-testkit'
    testImplementation 'com.ginsberg:junit5-system-exit:1.1.1'
    testImplementation 'com.github.blindpirate:junit5-capture-system-output-extension:0.1.2'
    // since dw 2.0.23 downgraded to 1.3 but 2.1 required by junit5-capture-system-output-extension
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    // junit 4 used by spock (waiting 2.0) and legacy junit4 extensions tests
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs {
    publish {
        docPath = '5.3.0'
        rootRedirect = true
    }
    extras = [
            'version': '5.3.0',
            'ext': '5.3.0-1',
            'dropwizard': project.dropwizard,
            'guice': project.guice
    ]
}

pom {
    delegate.properties {
        'guice.version' guice
        'dropwizard.version' dropwizard
        'hk2.version' hk2
    }
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

javaLib {
    // java 9 auto module name
    autoModuleName = 'ru.vyarus.dropwizard.guicey'
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
    // put resolved dependencies versions
    pom.forceVersions()
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    if (System.getenv("TRAVIS")) {
        // tests run only on windows build due to untraceable failure on travis
        exclude "**/HKScopeTest.class"
        exclude "**/HkFirstModeScopeTest.class"
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'

task updateGithubPom(type: Copy, group: 'other') {
    from(generatePomFileForMavenPublication)
    into '.github'
    rename 'pom-default.xml', 'pom.xml'
}