plugins {
    id 'com.github.ben-manes.versions' version '0.48.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '3.0' apply false
}

wrapper {
    gradleVersion = '8.3'
}

description = 'Dropwizard guicey examples'

ext {
    guiceyBom = '6.1.2'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'project-report'

    java {
        sourceCompatibility = 11
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories { mavenLocal(); mavenCentral(); maven { url 'https://jitpack.io' }}
    dependencies {
        implementation platform("ru.vyarus.guicey:guicey-bom:$guiceyBom")

        constraints {
            implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:5.1.3'

            implementation 'org.flywaydb:flyway-core:9.22.2'
            // flyway not compatible with h2 1.4.200 anymore
            implementation 'com.h2database:h2:2.2.224'
        }

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'ru.vyarus:spock-junit5:1.2.0'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
        testImplementation "io.dropwizard:dropwizard-testing"
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
        maxHeapSize = "512m"
    }

}

dependencyUpdates.revision = 'release'
