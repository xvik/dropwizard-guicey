plugins {
    id 'ru.vyarus.github-info' version '1.5.0' apply false
    id 'ru.vyarus.quality' version '4.9.0' apply false
    id 'org.cyclonedx.bom' version '1.8.2' apply false
    id 'ru.vyarus.mkdocs' version '3.0.0' apply false

    id 'jacoco'
    id 'java-platform'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'net.researchgate.release' version '3.0.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

wrapper {
    gradleVersion = '8.4'
}

description = 'Dropwizard guice integration'

ext {
    dropwizard = '3.0.6'
    guice = '6.0.0'
    hk2 = '2.6.1'
    spockJunit5 = '1.2.0'
    guiceExtAnn = '1.4.1'
    guiceValidator = '2.0.1'
}

// root project is a BOM
dependencies {
    // dropwizard BOM must go first to let maven select correct guava
    api platform("io.dropwizard:dropwizard-dependencies:$dropwizard")
    api platform("com.google.inject:guice-bom:$guice")

    constraints {
        api "com.google.inject:guice:$guice"
        api "org.glassfish.hk2:guice-bridge:$hk2"
        api "ru.vyarus:spock-junit5:$spockJunit5"

        api "ru.vyarus:guice-ext-annotations:$guiceExtAnn"
        api "ru.vyarus:guice-validator:$guiceValidator"

        // add subprojects to BOM
        project.subprojects.each { api it }
    }
}

// Required fix to workaround MAVEN behaviour: dropwizard BOM declares jakarta.inject as 1.0.5
// and later guice BOM, declaring jakarta.inject as 2.0.1 is IGNORED by maven (for imported BOM only the first
// declaration counted)
// This could be fixed by reordering dropwizard and guice BOMs, but I can't do it because dropwizard BOM must
// be prioritized in order to bring more recent guava version (as dropwizard release more often, it often require
// mote recent guava version).
// So the fix is to put required jakarta.inject version BEFORE dropwizard and guice BOMs inclusion.
withPomXml {
    def deps = it.dependencyManagement[0].dependencies[0]
    def jakarta = deps.appendNode('dependency')
    jakarta.with {
        appendNode('groupId', 'jakarta.inject')
        appendNode('artifactId', 'jakarta.inject-api')
        appendNode('version', '2.0.1')
    }

    deps.remove(jakarta)
    deps.children().add(0, jakarta)
}

javaLib {
    // aggregated test and coverage reports
    aggregateReports()
    // publish root BOM as custom artifact
    bom {
        artifactId = 'guicey-bom'
        description = 'Guicey BOM'
    }
}

pom {
    delegate.properties {
        'guice.version' guice
        'dropwizard.version' dropwizard
        'hk2.version' hk2
        'guice-ext-annotations.version' guiceExtAnn
    }
}

// maven publication related configuration applied to all projects
allprojects {
    apply plugin: 'project-report'
    apply plugin: 'ru.vyarus.github-info'
    apply plugin: 'ru.vyarus.java-lib'
    apply plugin: 'signing'

    repositories { mavenLocal(); mavenCentral(); maven { url 'https://jitpack.io' } }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    group = 'ru.vyarus.guicey'

    github {
        user = 'xvik'
        license = 'MIT'
        repository = 'dropwizard-guicey'
    }

    // delay required because java plugin is activated only in subprojects and without it
    // pom closure would reference root project only
    afterEvaluate {
        pom {
            developers {
                developer {
                    id 'xvik'
                    name 'Vyacheslav Rusakov'
                    email 'vyarus@gmail.com'
                }
            }
        }
    }

    // don't publish gradle metadata artifact
    javaLib.withoutGradleMetadata()

    // skip signing for jitpack (snapshots)
    tasks.withType(Sign) { onlyIf { !System.getenv('JITPACK') } }
}

// all sub-modules are normal java modules, using root BOM (like maven)
subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'ru.vyarus.quality'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.cyclonedx.bom'

    java {
        sourceCompatibility = 11
    }

    dependencies {
        implementation platform(project(':'))

        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.0'
    }

    if (project.name != 'dropwizard-guicey') {
        // extra modules use different group
        group = 'ru.vyarus.guicey'

        // common dependencies for all modules except core
        dependencies {
            implementation project(':dropwizard-guicey')

            testImplementation 'io.dropwizard:dropwizard-testing'
        }

        javaLib {
            // java 9 auto module name
            autoModuleName = "$group.${project.name.replace('guicey', 'ru.vyarus.dropwizard.guicey').replace('-', '.')}"
            // use only direct dependencies in the generated pom, removing BOM mentions
            pom.removeDependencyManagement()
        }
    }

    test {
//        useJUnitPlatform()
        testLogging {
            events 'skipped', 'failed'
            exceptionFormat 'full'
        }
        maxHeapSize = '512m'
    }

    dependencyUpdates.revision = 'release'

    if (!project.name.startsWith('guicey-test-')) {
        test {
            useJUnitPlatform()
        }
        dependencies {
            testImplementation 'ru.vyarus:spock-junit5'
            testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
            testImplementation 'org.junit.jupiter:junit-jupiter-api'
        }
    } else {
        // don't compile and run tests for legacy junit4 and spock1 modules
        tasks.withType(GroovyCompile)
                .configureEach { it.onlyIf { JavaVersion.current() <= JavaVersion.VERSION_11 } }
        test.onlyIf { JavaVersion.current() <= JavaVersion.VERSION_11 }
    }

    // SBOM
    cyclonedxBom {
        includeConfigs = ["runtimeClasspath"]
        destination = file("build/reports")
        outputName = "bom"
        outputFormat = "all"
    }
    publishing.publications.maven {
        artifact(file('build/reports/bom.json')) {
            classifier = 'cyclonedx'
            builtBy cyclonedxBom
        }
        artifact(file('build/reports/bom.xml')) {
            classifier = 'cyclonedx'
            builtBy cyclonedxBom
        }
    }
}

// dependency on all subprojects required for release validation
check.dependsOn subprojects.check

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

release.git.requireBranch.set('dw-3')

// release manages only root project (subprojects will be checked and released implicitly)
afterReleaseBuild {
    dependsOn 'publishToSonatype'
    dependsOn subprojects.collect { ":$it.name:publishToSonatype" }
    dependsOn 'closeAndReleaseSonatypeStagingRepository'
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

