plugins {
    id 'com.gradle.build-scan' version '3.1'
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.1.2'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.quality' version '4.0.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'ru.vyarus.mkdocs' version '1.1.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '5.6.4'
}

ext {
    dropwizard = '2.0.0-rc13'
    guice = '4.2.2'
    hk2 = '2.6.1'
    groovy = '2.5.7'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencyManagement {
    imports {
        mavenBom "com.google.inject:guice-bom:$guice"
        mavenBom "io.dropwizard:dropwizard-bom:$dropwizard"
        mavenBom "io.dropwizard:dropwizard-dependencies:$dropwizard"
    }
    // exclusions here mostly fixes conflicts for maven projects
    dependencies {
        // force guava version from dropwizard bom
        dependency "com.google.guava:guava:${dependencyManagement.importedProperties['guava.version']}"
        dependency "org.glassfish.hk2:guice-bridge:$hk2", {
            exclude 'com.google.inject:guice'
            exclude 'org.glassfish.hk2:hk2-api'
        }
        dependency 'org.spockframework:spock-core:1.2-groovy-2.5'
        // groovy bundled with spock (2.5.2) does not work well with java 11
        dependency "org.codehaus.groovy:groovy:$groovy"
        dependency "org.codehaus.groovy:groovy-json:$groovy"
        dependency "org.codehaus.groovy:groovy-macro:$groovy"
        dependency "org.codehaus.groovy:groovy-nio:$groovy"
        dependency "org.codehaus.groovy:groovy-sql:$groovy"
        dependency "org.codehaus.groovy:groovy-templates:$groovy"
        dependency "org.codehaus.groovy:groovy-test:$groovy"
        dependency "org.codehaus.groovy:groovy-xml:$groovy"

        dependency 'com.github.stefanbirkner:system-rules:1.19.0'

        dependency "com.google.inject:guice:$guice", { exclude 'com.google.guava:guava' }

        // add guicey itself to BOM (for version management)
        dependency 'ru.vyarus:dropwizard-guicey:${project.version}'
    }
}
dependencies {
    provided 'junit:junit'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.spockframework:spock-core'
    provided 'com.github.stefanbirkner:system-rules'
    provided 'com.github.spotbugs:spotbugs-annotations:3.1.2'
    provided "org.glassfish.hk2:guice-bridge"

    compile 'com.google.inject:guice'
    compile 'com.google.inject.extensions:guice-servlet'
    compile 'io.dropwizard:dropwizard-core'
    compile 'ru.vyarus:generics-resolver:3.0.1'

    testCompile 'io.dropwizard:dropwizard-auth'
    testCompile 'org.glassfish.jersey.ext:jersey-proxy-client'
    testCompile 'cglib:cglib-nodep:3.2.9'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
    vcsUrl = 'https://github.com/xvik/dropwizard-guicey'
}

python.pip 'mkdocs:1.0.4',
        'mkdocs-material:4.4.2',
        'WeasyPrint:49', // bug in version 50 (https://github.com/Kozea/WeasyPrint/issues/953)
        'mkdocs-pdf-export-plugin:0.5.5'
mkdocs.publish {
    docPath = '5.0.0-rc.3'
    rootRedirect = false
}

pom {
    delegate.properties {
        'guice.version' guice
        'dropwizard.version' dropwizard
        'hk2.version' hk2
    }
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

jar {
    manifest {
        attributes 'Automatic-Module-Name': 'dropwizard-guicey.core'
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'guice']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    if (System.getenv("TRAVIS")) {
        // tests run only on windows build due to untraceable failure on travis
        exclude "**/HKScopeTest.class"
        exclude "**/HkFirstModeScopeTest.class"
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}
