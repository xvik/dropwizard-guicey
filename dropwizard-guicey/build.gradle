plugins {
    id 'java-library'
    id 'ru.vyarus.mkdocs'
}

dependencies {
    provided 'org.junit.jupiter:junit-jupiter-api'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.glassfish.jersey.media:jersey-media-multipart'
    provided ('org.glassfish.hk2:guice-bridge') {
        exclude group: 'com.google.inject', module: 'guice'
    }

    api ('com.google.inject:guice') {
        artifact {
            classifier = 'classes'
        }
        // use dropwizard guava version
        exclude group: 'com.google.guava', module: 'guava'
    }
    api 'org.ow2.asm:asm'
    api ('com.google.inject.extensions:guice-servlet') {
        exclude group: 'com.google.inject', module: 'guice'
    }
    api 'io.dropwizard:dropwizard-core'
    api 'ru.vyarus:generics-resolver:3.0.3'

    optional 'org.mockito:mockito-core'

    testImplementation 'org.glassfish.jersey.inject:jersey-hk2'
    testImplementation 'io.dropwizard:dropwizard-auth'
    testImplementation 'io.dropwizard:dropwizard-forms'
    testImplementation 'org.glassfish.jersey.ext:jersey-proxy-client'
    testImplementation 'org.junit.platform:junit-platform-testkit'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'
    testImplementation 'com.google.truth:truth:1.4.4'
    testImplementation 'org.objenesis:objenesis:3.4'
    // test rest stubs
    //testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2'

    // required for pure junit 5 tests
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter'
}

// core module group is different!
group = 'ru.vyarus'
description = 'Dropwizard guice integration'

javaLib {
    // java 9 auto module name
    autoModuleName = 'ru.vyarus.dropwizard.guicey'
}

// use only direct dependencies in the generated pom, removing BOM mentions
maven.removeDependencyManagement()

mkdocs {
    publish {
        docPath = '7.3.0'
        rootRedirect = false
//        rootRedirectTo = 'latest'
//        versionAliases = ['latest']
        hideOldBugfixVersions = true
    }
    extras = [
            'version': '7.3.0',
            'dropwizard': rootProject.dropwizard,
            'guice': rootProject.guice
    ]
}